plugins {
    id("com.android.compose.screenshot")
    id("com.android.library")
    id("kotlin-android")
    id("maven-publish")
    id("org.jetbrains.kotlin.plugin.compose")
    id("org.jlleitschuh.gradle.ktlint")
}

android {
    namespace = "com.heckofanapp.search"

    defaultConfig {
        compileSdk = 36
        minSdk = 28
        targetSdk = 36
    }

    buildFeatures {
        compose = true
    }

    compileOptions {
        sourceCompatibility = "$version_java"
        targetCompatibility = "$version_java"
    }

    ktlint {
        android = true
        ignoreFailures = false
        verbose = true
    }

    lint {
        baseline = file("lint_baseline.xml")
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }

    libraryVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            output.outputFileName = "${namespace}-${version_code}(${version_name}).aar"
        }
    }

    experimentalProperties["android.experimental.enableScreenshotTest"] = true
}

Properties properties = new Properties()

if (file("${rootDir}/publish.properties").canRead()) {
    properties.load(file("${rootDir}/publish.properties").newDataInputStream())
}

apply from: file("${rootDir}/publish.gradle")

afterEvaluate {
    publishLibraryModule("search", "$version_name", properties.getProperty("password"), properties.getProperty("username"))
}

dependencies {
    implementation platform("androidx.compose:compose-bom:${version_compose}")

    implementation("androidx.compose.animation:animation-graphics")
    implementation("androidx.compose.material3:material3")
    implementation("androidx.compose.ui:ui")
    implementation("androidx.compose.ui:ui-tooling-preview")
    implementation("androidx.constraintlayout:constraintlayout-compose:1.1.1")

    debugImplementation("androidx.compose.ui:ui-tooling")

    screenshotTestImplementation("androidx.compose.ui:ui-tooling")
    screenshotTestImplementation("com.android.tools.screenshot:screenshot-validation-api:${version_test}")
}
